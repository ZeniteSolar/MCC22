* PSpice Model Editor - Version 16.5.0
*$
.SUBCKT FDMS86255 2 1 3
+ params: tau=30 igain=1300 fs=890
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                      FDMS86255 Spice model                   **
**                      Revision RevA, 02 Jul 2013              **
******************************************************************			
*Nom Temp 25 deg C		
Dbreak 5 11 DbreakMOD 		
Lgate 1 9 0.853e-9		
Ldrain 2 5 0.004e-9		
Lsource 3 7 0.033e-9		
RLgate 1 9 8.53		
RLdrain 2 5 0.04		
RLsource 3 7 0.33		
Rgate 9 6 0.29
* trr/qrr module
H1   161 141 V_H1 {tau}
V_H1 151 7 0V
V10  5 161 0
F1   7 161 V_F1 {-igain}
V_F1 171 141 0V
D2   151 161 DbodyMOD
.MODEL DbodyMOD D (IS=5e-12 n=1 RS=0.85e-3 TRS1=2.2e-3 TRS2=1e-6
+ CJO=1e-9 M=0.55 TT=0 XTI=3) 
R9   131 171  {tau*fs}
C2   161 131  1p					
* Shielded  Gate  		
D_D1 100 5 D_SG_cap		
D_D2 100 101 D_SG_cap		
R_R1 101 7 0.03		
C_C1 6 101 265e-12		
.MODEL D_SG_cap D (IS=1e-9 n=1 RS=5e-3 CJO=2.68e-9 M=0.6 t_abs=25) 		
It 7 17 1		
Ebreak 11 7 17 7 160.7		
Rbreak 17 7 RbreakMOD 1 		
.MODEL RbreakMOD RES (TC1=0.74e-3 TC2=-1e-6)		
.MODEL DbreakMOD D (RS=8e-3 TRS1=1e-3 TRS2=1e-6 )		
Rsource 7a 7 0.15e-3		
Rdrain 5 16 RdrainMOD 7.9e-3		
.MODEL RdrainMOD RES (TC1=8.5e-3 TC2=29e-6)		
M_BSIM3 16 6 7a 7a Bsim3 W=7.58 L=1.1e-6 NRS=0 NRD=0		
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0		
*Process Parameters		
+ TOX=1000e-10 ;Oxide thickness		
+ XJ=0.54e-6 ;Channel depth		
+ NCH=0.77e17 ;Channel concentration		
*Channel Current		
+ U0=610 VSAT=500000 DROUT=1.8		
+ DELTA=0.05 PSCBE2=0 RSH=0.15e-3		
*Threshold voltage		
+ VTH0=3.75		
*Sub-threshold characteristics		
+ VOFF=-0.37 NFACTOR=1		
*Junction diodes and Capacitance		
+ LINT=0.19e-6 DLC=0.19e-6 		
+ CGSO=137e-12 CGSL=0 CGDO=0.2e-12 CGDL=105e-12 ; 172		
+ CJ=0 CF=0 CKAPPA=0.25		
* Temperature parameters 		
+ KT1=-1.75 KT2=0 UA1=2.5e-9		
+ NJ=10)		
.ENDS   		
* 		
*$
